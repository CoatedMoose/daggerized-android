apply plugin: 'com.android.library'

apply plugin: 'maven'
apply plugin: 'signing'

group = "com.coatedmoose.android-utils"
archivesBaseName = "daggerized-android"
version = "1.0.0"

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 21
        versionCode 1
        versionName "$version"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    provided 'com.android.support:appcompat-v7:21.0.3'

    provided 'com.squareup.dagger:dagger:1.2.2'
    provided 'com.squareup.dagger:dagger-compiler:1.2.2'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                authentication(userName: "$ossrhUsername",
                        password: "$ossrhPassword")
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Daggerized Android'
                packaging 'aar'
                description 'Base Android classes with Dagger 1 hooks'
                url 'https://github.com/CoatedMoose/daggerized-android'

                scm {
                    url 'scm:git@github.com:CoatedMoose/daggerized-android.git'
                    connection 'scm:git@github.com:CoatedMoose/daggerized-android.git'
                    developerConnection 'scm:git@github.com:CoatedMoose/daggerized-android.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'CoatedMoose'
                        name 'Andrew Crichton'
                        email 'andrew@coatedmoose.com'
                    }
                }
            }
        }
    }
}
